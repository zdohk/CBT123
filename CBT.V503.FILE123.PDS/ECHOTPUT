ECHOTPUT TITLE 'ECHOTPUT - DRAIN BROADCAST MESSAGES FOR A USERID.'
         SPACE 1
*        REGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
SP000    EQU   0
         SPACE 1
ECHOTPUT CSECT
         USING ECHOTPUT,R12
         B     BEGINN-*(,R15)
         DC    AL1(16),CL16'ECHOTPUT VER 1.0'
         DC    CL16' &SYSDATE &SYSTIME '
         DS    0H
BEGINN   STM   R14,R12,12(R13)
         LR    R12,R15
         LR    R11,R1              PRESERVE CPPL POINTER.
         GETMAIN RU,LV=DATALEN,SP=SP000,LOC=BELOW
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING DATAAREA,R13
         SPACE 1
         L     R1,16               POINT TO CVT.
         L     R1,0(,R1)           POINT TO TCB/ASCB WORDS
         L     R10,12(,R1)         POINT TO ASCB.
         L     R9,4(,R1)           POINT TO TCB.
         L     R1,X'B4'(,R9)       POINT TO JSCB.
         L     R2,X'108'(,R1)      POINT TO PSCB.
         LA    R2,0(,R2)           CLEAR HIGH BYTE.
         LTR   R2,R2               ANY PSCB?
         BZ    EOJ8                GET OUT.  NOT TSO.
         USING PSCB,R2
CHEKAUTH DS    0H             BYPASS AUTHORIZATION           PARS 06/99
CONT0    LTR   R11,R11             ANY CPPL?
         BZ    EOJ8                NO.  GET OUT
         L     R1,0(,R11)          PT TO CMD BUFFER.
         LTR   R1,R1               ANY CMD BUFFER?
         BZ    EOJ4                GET OUT IF NONE
         LH    R3,0(,R1)           LOAD LENGTH.
         CH    R3,=H'4'            ANYTHING?
         BNH   EOJ4                NO.  GET OUT.
         LR    R5,R3               LENGTH OF COMMAND + 4
         ICM   R5,14,FULLZERO      CLEAR HI ORDER 3 BYTES
         LH    R3,2(,R1)           LOAD OFFSET.
*        SR    R5,R3               SUBTRACT OFFSET FROM FULL LENGTH+4
         SH    R5,=H'4'            CORRECT FOR LENGTH OF CMDBUFR HEADER
         BC    8,EOJ0              ZERO. MESSAGE
         BC    5,EOJ8              LESS THAN ZERO OR OVERFLOW, OUT.
*        AR    R1,R3               POINT ...
         LA    R1,4(,R1)           ... TO CHAR.
         BCTR  R5,0                DECREMENT REG FOR EXECUTE
         B     AROUNDEX
MOVEID   MVC   MSGBUF(*-*),0(R1)   EXECUTED
AROUNDEX DS    0H
         MVC   MSGBUF,MSGBUF-1     CLEAR FIELD
         EX    R5,MOVEID           MOVE COMMAND INTO MESSAGE
         LA    R5,1(,R5)           UP R5 TO ITS FORMER VALUE
         TPUT  MSGBUF,(R5)
         B     EOJ0                NORMAL END
EOJ4     LA    R15,4               SET CC = 4.
EOJ      DS    0H                  BYE.
         LR    R1,R13
         L     R13,4(,R13)
         ST    R15,16(,R13)        PUT RC INTO PASSED SAVE AREA
         L     R14,12(,R13)
         FREEMAIN RU,LV=DATALEN,A=(R1),SP=SP000
         LM    R0,R12,20(R13)
         BR    R14
EOJ0     SR    R15,R15             SET CC = 0.
         B     EOJ                 CONT.
EOJ8     LA    R15,8               SET CC = 8.
         B     EOJ                 CONT
         EJECT
FULLZERO DC    F'0'
* - - - - - - - - - - - - - - - - - - - - - - - - *
* - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 3
         LTORG
         SPACE 3
DATAAREA DSECT
SAVE     DS    9D
         DC    C' '
MSGBUF   DC    CL255' '            MESSAGE BUFFER
         DS    CL72                PADDING
         DS    0F                  ALIGN ON FULLWORD
         SPACE 1
DATALEN  EQU   *-DATAAREA          LENGTH OF DATA AREA
         IKJUPT
         IKJPSCB
         END
